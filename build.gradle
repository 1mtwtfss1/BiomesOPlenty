buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net/' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.+', changing: true
    }
}

plugins {
    id "com.matthewprenger.cursegradle" version "1.4.0"
}

apply plugin: 'maven-publish'
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'idea'

repositories { mavenLocal() }

group = "com.github.glitchfiend"
archivesBaseName = "BiomesOPlenty"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

def getGitHash = {
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

version = "${minecraft_version}-${mod_version}.${System.getenv().BUILD_NUMBER}"

minecraft {
    mappings channel: 'official', version: minecraft_version

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client = {
            workingDirectory = project.file("run").canonicalPath
            source sourceSets.main
        }
        server = {
            workingDirectory = project.file("run").canonicalPath
            source sourceSets.main
        }
    }
}

allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xmaxerrs" << "10000"
        }
    }
}

// configure  the source folders
sourceSets {
    main {
        java {
            srcDir "src/main/java"
            srcDir "public/src/main/java"
        }
        resources {
            srcDir "src/main/resources"
            srcDir "public/src/main/resources"
        }
    }
}

processResources {
	duplicatesStrategy = DuplicatesStrategy.INCLUDE
}

jar {
    classifier = 'universal'
    manifest {
        attributes([
            "Specification-Title": "biomesoplenty",
            "Specification-Vendor": "Biomes O' Plenty",
            "Specification-Version": "1.1",
            "Implementation-Title": project.name,
            "Implementation-Version": project.version,
            "Implementation-Vendor" :"biomesoplenty",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
        ])
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:' + minecraft_version + '-' + forge_version
    implementation 'com.github.glitchfiend:TerraBlender:1.18.1-1.0.0.22:deobf'
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

task apiJar(type: Jar) {
    from(sourceSets.main.allJava) {
        include 'biomesoplenty/api/**'
    }
    from (sourceSets.main.output) {
        include 'LICENSE.txt'
        include 'biomesoplenty/api/**'
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    classifier = 'api'
}

def changelog_file = rootProject.file("build/BiomesOPlenty-${version}-changelog.txt")

curseforge {
    if (project.hasProperty('curseApiKey')) {
        apiKey = project.getProperty('curseApiKey')
        project {
            id = '220318'

            if (changelog_file.exists()) {
                changelog = changelog_file
            }

            releaseType = 'beta'
            addGameVersion minecraft_version

            mainArtifact(jar) {
                displayName = "Biomes O' Plenty ${version}"
            }

            addArtifact deobfJar
            addArtifact apiJar

            relations {
                requiredDependency 'terrablender'
            }
        }
    }
}

artifacts {
    if (changelog_file.exists()) {
        archives changelog_file
    }
    archives jar
    archives deobfJar
    archives apiJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact deobfJar
            artifact apiJar
            pom {
                name = 'BiomesOPlenty'
                description = "Adds over 50 new biomes, blocks, and more!"
                url = 'https://github.com/Glitchfiend/BiomesOPlenty'
                scm {
                    url = 'https://github.com/Glitchfiend/BiomesOPlenty'
                    connection = 'scm:git:git://github.com/Glitchfiend/BiomesOPlenty.git'
                    developerConnection = 'scm:git:git@github.com:Glitchfiend/BiomesOPlenty.git'
                }
                issueManagement {
                    system = 'github'
                    url = 'https://github.com/Glitchfiend/BiomesOPlenty/issues'
                }

                licenses {
                    license {
                        name = 'All Rights Reserved'
                    }
                }

                developers {
                    developer {
                        id = 'adubbz'
                        name = 'Adubbz'
                    }
                    developer {
                        id = 'forstride'
                        name = 'Forstride'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            url "https://maven.minecraftforge.net/releases/"
            authentication {
                basic(BasicAuthentication)
            }
            credentials {
                username = System.env.MAVEN_USER ?:'fake'
                password = System.env.MAVEN_PASSWORD ?:'news'
            }
        }
    }
}
